def repoWorkspace

pipeline {
  agent any
  tools {
    maven "maven-jenkins"
  }
  stages {
    stage('Checkout') {
      steps {
        script {

          echo "params.REPO_NAME: ${params.REPO_NAME}"
          echo "params.REPO_BRANCH: ${params.REPO_BRANCH}"
          echo "params.PAYLOAD: ${params.PAYLOAD}"

          def repository = params.REPO_NAME
          def branch = params.REPO_BRANCH

          if (params.PAYLOAD != '') {
          echo "El job fue ejecutado por medio de un evento de GitHub... Se procede a mapear los datos del payload"
          echo "Payload: ${env.payload}"

          def payload = readJSON text: env.payload

            repository = payload.repository
            branch = payload.ref
          }

          repoWorkspace = "${WORKSPACE}/${repository}"
          dir(repoWorkspace) {
            git branch: "${branch}", credentialsId: 'local-jenkins', url: "https://github.com/${REPO_USERNAME}/${repository}.git"
          }
        }
      }
    }
    stage('Build') {
      steps {
        dir("${repoWorkspace}") {
          sh 'mvn clean package'
        }
      }
    }
    stage('Test') {
      steps {
        dir("${repoWorkspace}") {
          sh 'mvn test'
        }
      }
    }
    stage("Scan & Quality Gate") {
      steps {
        dir("${repoWorkspace}") {
          withSonarQubeEnv(installationName: "SonarQubeServer") {
            sh "mvn sonar:sonar"
          }
          timeout(time: 2, unit: "MINUTES") {
            waitForQualityGate abortPipeline: true
          }
        }
      }
    }
    stage('Publish to Artifactory') {
      steps {
        dir("${repoWorkspace}") {
          script {
            def server = Artifactory.server 'ccplsolutions.loca.jfrog'
            def rtMaven = Artifactory.newMavenBuild()

            rtMaven.tool = 'maven-jenkins'
            rtMaven.deployer releaseRepo: 'ccpl-libs-release-local', snapshotRepo: 'ccpl-libs-snapshot-local', server: server
            rtMaven.resolver releaseRepo: 'ccpl-libs-release', snapshotRepo: 'ccpl-libs-snapshot', server: server

            def buildInfo = rtMaven.run pom: 'pom.xml', goals: 'clean install'

            server.publishBuildInfo buildInfo
          }
        }
      }
    }

  }
  post {
    success {
      slackSend message: "Build successfully - ${env.REPO_NAME} # ${env.BUILD_NUMBER} (<${env.BUILD_URL}|Open>)"
    }
    failure {
      slackSend message: "Build failed - ${env.REPO_NAME} # ${env.BUILD_NUMBER} (<${env.BUILD_URL}|Open>)"
    }
  }
}